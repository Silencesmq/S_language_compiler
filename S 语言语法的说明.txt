S 语言语法的BNF 表示
(1) <程序>→[<常量说明>][<变量说明>]<语句>
(2) <常量说明>→Const <常量定义>{，<常量定义>}；
(3) <常量定义>→<标识符>＝<无符号整数>
(4) <无符号整数>→<数字>{<数字>}
(5) <字母>→a|b|c| … |z
(6) <数字>→0|1|2| … |9
(7) <标识符>→<字母>{<字母>|<数字>}
(8) <变量说明>→Var <标识符>{，<标识符>}；
(9) <语句>→<赋值语句>|<条件语句>|<当循环语句>|<复合语句>|ε
(10) <赋值语句>→<标识符>＝<表达式>
(11) <表达式>→[＋|－]<项>{<加法运算符><项>}
(12) <项>→<因子>{<乘法运算符><因子>}
(13) <因子>→<标识符>|<无符号整数>|‘(’<表达式>‘)’
(14) <加法运算符>→＋|－
(15) <乘法运算符>→* |／
(16) <条件语句>→if <条件> then <语句>| if <条件> then <语句> else
<语句>
(17) <条件>→<表达式><关系运算符><表达式>
(18) <关系运算符>→＝＝|＜＝|＜|＞|＞＝|＜＞
(19) <当循环语句>→while <条件> do <语句>
(20) <复合语句>→begin <语句>{；<语句>} end
注：产生式中<、>括起的部分表示一个非终结符号，[、]括起的部分表示可
选项，{、}括起的部分表示可重复，符号 | 表示“或”。

词类属性值：
syn=12;//识别+  syn=13;//识别-   syn=14;//识别*  syn=15;//识别/ 
syn=23;//识别； syn=24;//识别（  syn=25;//识别） syn=26;//识别， 
syn=0;//识别#   syn=-1;//词法错误  
syn=22;//识别<> syn=21;//识别<= syn=20;//识别<   syn=19;//识别>= 
syn=18;//识别>  syn=17;//识别==  syn=16;//识别= 

S语言的上下文无关文法为：
<程序>→<G><H><语句>
<G>→<常量说明>
<G>→ε
<H>→<变量说明>
<H>→ε
<程序>→[<变量说明>]<语句>
<常量说明>→Const <常量定义><A>；
<A>→，<常量定义><A> 
<A>→ε
<常量定义>→<标识符>＝<无符号整数>
<变量说明>→Var <标识符><B>；
<B>→，<标识符><B>
<B>→ε
<语句>→<赋值语句>
<语句>→<条件语句>
<语句>→<当循环语句>
<语句>→<复合语句>
<语句>→ε
<赋值语句>→<标识符>＝<表达式>
<表达式>→＋<项><C>
<表达式>→－<项><C>
<表达式>→<项><C>
<C>→<加法运算符><项><C>
<C>→ε
<加法运算符>→＋
<加法运算符>→－
<项>→<因子><D>
<D>→<乘法运算符><因子><D>
<D>→ε
<乘法运算符>→* 
<乘法运算符>→／
<因子>→<标识符>
<因子>→<无符号整数>
<因子>→‘(’<表达式>‘)’
<条件语句>→if <条件> then <语句><E>
<E>→else<语句>
<E>→ε
<条件>→<表达式><关系运算符><表达式>
<关系运算符>→＝＝
<关系运算符>→＜＝
<关系运算符>→＜
<关系运算符>→＞
<关系运算符>→＞＝
<关系运算符>→＜＞
<当循环语句>→while <条件> do <语句>
<复合语句>→begin <语句><F>end
<F>→；<语句><F>
<F>→ε
